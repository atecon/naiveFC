include naiveFC_helper.inp

function bundle dict_models (void)
/* Dictionary including stuff for calling estimation
    command and fcast command. */

    bundle dict_models

    dict_models.meanfc = defbundle( \
      "forecast_arg", "--static", \
      "command", "ols y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )
    dict_models.medianfc = defbundle( \
      "forecast_arg", "--static", \
      "command", "quantreg 0.5 y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )
    dict_models.smeanfc = defbundle( \
      "forecast_arg", "--static", \
      "command", "ols y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 1 \
      )
    dict_models.smedianfc = defbundle( \
      "forecast_arg", "--static", \
      "command", "quantreg 0.5 y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 1 \
      )
    dict_models.snaivefc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "arima 0 0 0 ; 0 1 0 ; y xlist --nc", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )
    dict_models.snaivedriftfc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "arima 0 0 0 ; 0 1 0 ; y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )
    dict_models.rwfc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "arima 0 1 0 ; y xlist --nc", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 0, \
      "add_ohe_periods", 0 \
      )
    dict_models.rwdriftfc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "arima 0 1 0 ; y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 0, \
      "add_ohe_periods", 0 \
      )
    dict_models.ar1fc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "ols y xlist", \
      "add_linear_trend", 0, \
      "add_first_lag", 1, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )
    dict_models.ar1trendfc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "ols y xlist", \
      "add_linear_trend", 1, \
      "add_first_lag", 1, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )
    dict_models.ar1ma1fc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "arima 1 0 1 ; y xlist",
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )

    # Harvey (1989, sect. 2.5.5) has shown that the autocorrelation structure
    # of the airline model is similar to that of an UCM composed by LLT and
    # stochastic seasonals.
    dict_models.airlinefc = defbundle( \
      "forecast_arg", "--dynamic", \
      "command", "arima 0 1 1 ; 0 1 1 ; y xlist",
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 1, \
      "add_ohe_periods", 0 \
      )

    dict_models.bsmfc = defbundle( \
      "forecast_arg", "", \
      "command", "BSM(y, 0, &model)", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 0, \
      "add_ohe_periods", 0 \
      )

    dict_models.lltfc = defbundle( \
      "forecast_arg", "", \
      "command", "LLT(y, 0, &model)", \
      "add_linear_trend", 0, \
      "add_first_lag", 0, \
      "add_intercept", 0, \
      "add_ohe_periods", 0 \
      )

    return dict_models
end function


function bundle default_values (const series y)
    /* Set default values */

    bundle self = null
    scalar self.error = FALSE
    scalar self.y_is_constant = FALSE
    list self.xlist = null
    scalar self.endogenous_in_logs = FALSE
    scalar self.horizon = 10				# maximum forecast horizon
    scalar self.level = 0.9					# probability level for forecast interval
    scalar self.fan = FALSE						# plot fan chart  (not supported yet)
    scalar self.nboot = 0					# no. of bootstrap replications  (not supported yet)
    scalar self.blength = 4					# Block length bootstrap  (not supported yet)
    scalar self.is_seas = data_has_seasonality()
    strings self.supported_estimators = get_estimator_names(self.is_seas)
    scalar self.verbose = 1

    genr index
    series self.index = index

    string self.type_moving_window = "static" 			# "static", "rolling" or "recursive"
    scalar self.window_length = ceil(0.25 * nobs(y))	# default window length for moving-windows

    # For naivePlot()
    string self.title = ""
    string self.ylab = ""
    string self.xlab = ""
    string self.filename = "display"
    scalar self.pre_fc_periods_to_plot = int(0.25 * nobs(y))

    return self
end function



# TODO: untested
function list prepare_xlist (const bundle model_settings,
                             const bundle self)
    /* Prepare list of additional regressors. */

    list xlist = null

    if model_settings.add_ohe_periods
        xlist += add_ohe_periods_to_list()
    endif
    if model_settings.add_intercept && !inlist(self.xlist, "const")
        xlist += add_intercept_to_list()
    endif
    if model_settings.add_linear_trend
        xlist += add_linear_trend_to_list()
    endif

    if inbundle(self, "xlist")
        xlist += self.xlist
    endif

    xlist = add_lagged_y_to_xlist(self.y, xlist, model_settings)

    # TODO: check for duplicates in xlist

    return xlist
end function


function string add_quiet_str_to_command (const bundle self,
                                        const bundle model_settings)
  /* Retrieve command string for estimation and optionally attache "quiet" string. */

  string command = model_settings.command
  command += (self.verbose < 2 && model_is_ucm(self) == FALSE) ? " --quiet" : ""  # estimation details only for detailed verbosity

  return command
end function

function string add_quiet_str_to_fcast_arg (const bundle self,
                                          const bundle model_settings)
  /* Retrieve command string for getting forecast values and optionally attache "quiet" string. */

  string fcast_arg = model_settings.forecast_arg
  fcast_arg += (self.verbose < 2 && model_is_ucm(self) == FALSE) ? " --quiet" : ""

  return fcast_arg
end function

function list add_lagged_y_to_xlist (series y,
                                    list xlist,
                                    const bundle model_settings)
  /* Conditionally add 1st lag of endogenous to xlist. */

  if model_settings.add_first_lag == TRUE
    xlist += y(-1)
  endif

  return xlist
end function

# TODO: untested
function matrix set_xlist_and_forecast (bundle *self)
  /* Set list of regressors, estimate model and compute point forecast and forecast standard errors. */

  matrix forecast_results = mshape(NA, self.horizon, 2)
  series y = self.y         # length: window_length + horizon
  bundle model_settings = dict_models()[self.active_model]
  string command = add_quiet_str_to_command(self, model_settings)
  string fcast_arg = add_quiet_str_to_fcast_arg(self, model_settings)
  list xlist = prepare_xlist(model_settings, self)

  # "(train_obsnums.t2_obsnum - train_obsnums.t1_obsnum)"
  # is not the effective sample length but (window_length-max(lags))
  bundle train_obsnums = get_train_start_end_obsnums(self)
  bundle test_obsnums = get_test_start_end_obsnums(train_obsnums, self)

  # Check training and test samples
  if sample_is_valid(train_obsnums.t1_obsnum, train_obsnums.t2_obsnum) == FALSE
    scalar self.error = TRUE
    return forecast_results
  endif
  smpl train_obsnums.t1_obsnum train_obsnums.t2_obsnum

  # TODO: Can this happen, and if so why -- otherwise drop
  if window_length_is_ok(self) == FALSE
    scalar self.error = TRUE
    return forecast_results
  endif

  # Check series
  scalar self.y_is_constant = series_isconst(y)
  if self.y_is_constant == TRUE
      return forecast_results
  endif
  list xlist_new = drop_zero_series_from_list(xlist)
  print_dropped_series(xlist, xlist_new, self)
  list xlist = xlist_new

  if model_is_ucm(self) == TRUE
    forecast_results[1:self.horizon_current,] = estimate_and_forecast_structism(command, y, train_obsnums, &self)
  else
    smpl full     # samples are set within function
    forecast_results[1:self.horizon_current,] = estimate_and_forecast_gretl(command, fcast_arg, y, xlist, train_obsnums, test_obsnums, &self)
  endif

  return forecast_results
end function


# TODO: put checks into separate functions
function void do_initial_checks (bundle *self)
    /* Helper function perfoming some initial checks */

    pkg query extra --quiet

    if $result.version < 0.6
        Error("You need at least version 0.6 of the 'extra' package.")
        Error("Update by executing 'pkg install extra'")
    endif

    strings methods = get_supported_nonseas_methods()
    strings seas_methods = get_supported_seas_methods()

    # Check whether called method exists
    if !nelem(instrings(methods, self.which)) && !nelem(instrings(seas_methods, self.which))
        Error("The forecasting method you selected does not exist.")
        self.error = 1
    endif

    if !self.is_seas && nelem(instrings(seas_methods, self.which))
        Error("The method you requested is not available for non-seasonal data.")
        self.error = 1
    endif

    # TODO: Why is this necessary??
    if self.window_length < $pd
        Error(sprintf("Window size = %d", self.window_length))
        Error(sprintf("Periodicity = %d\n", $pd))
        Error("The size of the moving window cannot be smaller than the underlying periodicity.")
        #        self.error = 1		# TODO: don't return error as long as it is not clear why/ when this case matters
    endif

    if self.nboot
        print_no_bootstrap_support()
        self.error = 1
    endif
end function


function void xlist_is_valid (bundle *self)
    /* Flag whether the requested method supports exogenous variables. */

    strings not_valid_for = defarray("rwfc", "rwdriftfc", "snaivefc", "snaivedriftfc", "bsmfc", "lltfc")
    matrix not_supported = instrings(not_valid_for, self.which)

    if nelem(self.xlist) > 0 && nelem(not_supported) > 0
        Warn(sprintf("No exogenous variables can be included for the selected model '%s'.", self.which))
        Info("List of exogenous will be cleared. Proceed.")
        list self.xlist = null
    endif
end function


# TODO: untested
function void t1_and_t2_of_y_and_xlist (const series y,
                                        bundle *self)
    /* Check valid start and end dates of y and xlist. */

    list xlist = self.xlist
    scalar t1_curr = $t1
    scalar t2_curr = $tmax
    bundle obsdetails_y
    bundle obsdetails_x

    obslabels_and_nobs_to_bundle(y, &obsdetails_y)
    string self.t1_date_y = obsdetails_y.t1_date
    string self.t2_date_y = \
      obslabel(obsnum(obsdetails_y.t2_date) - self.n_sampleend_obs_replaced)
    scalar self.t1_obsnum_y = obsnum(self.t1_date_y)
    scalar self.t2_obsnum_y = obsnum(self.t2_date_y)

    scalar t1_diff_y = self.t1_obsnum_y - t1_curr
    scalar t2_diff_y = t2_curr - self.t2_obsnum_y

    if t1_diff_y > 0
        Warn(sprintf("Found %d missing initial observations for %s.", \
          t1_diff_y, self.name_endo))
    endif

    if nelem(xlist)
        obslabels_and_nobs_to_bundle(xlist, &obsdetails_x)
        scalar self.t1_obsnum_xlist = obsnum(obsdetails_x.t1_date)
        scalar self.t2_obsnum_xlist = obsnum(obsdetails_x.t2_date)
        string self.t1_date_xlist = obsdetails_x.t1_date
        string self.t2_date_xlist = obsdetails_x.t2_date
        scalar t1_diff_x = self.t1_obsnum_xlist - t1_curr
        scalar t2_diff_x = t2_curr - self.t2_obsnum_xlist

        if t1_diff_x > 0
            Warn(sprintf("Found %d missing initial observations for exogenous.",\
              t1_diff_x))
        endif
        if t2_diff_x > 0
            Warn(sprintf("Found %d missing observations at sample end for exogenous.",\
              t2_diff_x))
        endif
    endif
end function


function bundle naiveFC (series y,
                         string which "Select forecasting method",
                         bundle opts[null] "Bundle incl. optional parameters")
    /* Main package function */

    if !exists(opts)
        bundle opts = null
    endif
    bundle self = set_bundle(y, which, opts)

    do_initial_checks(&self)	# TODO: return scalar capturing error instead of using pointer!
    if self.error
        return self
    endif

    xlist_is_valid(&self)

    # Replace of missing at the sample end
    # must be done here before calling t1_and_t2_of_y_and_xlist()
    scalar n_sampleend_obs_replaced = NA
    series y = repl_sampleend_missings_of_y(y, self.verbose,\
                                            &n_sampleend_obs_replaced)
    self.n_sampleend_obs_replaced = n_sampleend_obs_replaced

    t1_and_t2_of_y_and_xlist(y, &self)

    if self.type_moving_window == "static"
        # "self.t2_date_y" refers to in-sample end date of "y"
        scalar self.window_length = smplspan(self.t1_date_y, self.t2_date_y, $pd)
    endif

    scalar check = valid_dates_of_y_and_xlist(&self)
    if check
        Error("Problem with dating for y and/ or xlist.")
        return self
    endif

    scalar check = sufficient_future_obs(&self)
    if check
        return self
    endif

    # get_sample_end(self) = in-sample end plus max(horizon)
    smpl obsnum(self.t1_date_y) get_sample_end(self)

    series self.y = endogenous_series_in_logs(y, &self)
    if self.error == TRUE
        return self
    endif

    do_naive_forecasting(&self)

    if self.which == "avgfc"
        self.forecast_results.avgfc = compute_avgerage_forecast(&self)
    endif

    summarize_naive_fc_results(&self)

    if self.endogenous_in_logs
        series self.y = exp(self.y)
    endif

    cleanup_bundle(&self)

    return self
end function


function void cleanup_bundle (bundle *self)
    /* Delete some elements before returning to user. */

    delete self.fan
    delete self.blength
    delete self.nboot
    if inbundle(self,"obs_season")
        delete self.obs_season
    endif
    delete self.supported_estimators
    delete self.index
    delete self.active_model
    delete self.horizon_current
    delete self.t1_date_y
    delete self.t2_date_y
    delete self.xlist
end function


function bundle prepare_forecast_bundles (bundle *self)
/* Prepare for each estimator a dictionary for point
    interval forecast results to be stored in matrices. */

    bundle forecast_results

    loop i=1..nelem(self.models) -q
        string model = self.models[i]
        bundle forecast_results["@model"] = null
    endloop

    return forecast_results
end function


function void do_naive_forecasting (bundle *self)
    /* Wrapper function actually calling procedures. */

    strings models = getkeys(self.forecast_results)

    loop i=1..nelem(self.forecast_results) -q	# loop over models
        self.active_model = models[i]

        bundle interval_forecasts = execute_forecasting(&self)

        attach_row_labels(&interval_forecasts, self)
        attach_column_labels(&interval_forecasts, self)

        self.forecast_results[self.active_model] = interval_forecasts
    endloop
end function


function void summarize_naive_fc_results (bundle *self)
    /* Print summarising information */

    if self.verbose
        printf "\n-------------------------------------------------------------------\n"
        printf "\t\tSummary of naive forecasting\n"
        printf "\nForecasting method: \t\t\t%s\n", self.which
        printf "Endogenous: \t\t\t\t%s\n", self.name_endo
        if self.endogenous_in_logs
            printf "Estimates based on logarithms of %s\n", self.name_endo
        endif
        printf "Number of additional regressors: \t%d\n", nelem(self.xlist)
        printf "In-Sample: \t\t\t\t%s to %s (T = %d)\n", self.t1_date_y, \
          self.t2_date_y, nobs(self.y) - self.horizon
        printf "Max. forecast horizon: \t\t\t%d\n", self.horizon
        if self.type_moving_window == "static"
            printf "Last in-sample observation: \t\t%s\n", self.t2_train_date
        elif self.type_moving_window == "rolling"
            printf "Moving window length: \t\t\t%d\n", self.window_length
        else
            printf "Initial window length: \t\t\t%d\n", self.window_length
        endif
        if self.type_moving_window != "static"
            printf "Number of '%s' multi-step forecasts: %d\n", self.type_moving_window, \
              self.n_folds
            printf "First observation forecasted: \t\t%s\n", self.t1_forecast_date
            printf "Last observation forecasted: \t\t%s\n", self.t2_forecast_date
        else
            printf "First observation forecasted (h=1): \t%s\n", self.t1_forecast_date
            printf "Last observation forecasted (h=%d): \t%s\n", self.horizon, \
              self.t2_forecast_date
        endif
        if self.type_moving_window == "static"
            printf "Width forecast interval: \t\t%d pct.\n", self.level * 100
        else
        endif
        printf "-------------------------------------------------------------------\n\n"
    endif
end function


function bundle execute_forecasting (bundle *self)
/* This function estimates the point forecasts either in (i) a rolling or
    (ii) a recursive manner and stores sequences of interval forecasts. */

    bundle cv = prep_and_get_cv_folds(self)
    self.n_folds = cv.n_folds

    # Loop over training sets, run estimation and retrieve interval forecasts.
    # returns an n_folds-dimensional array comprising h by 2 (point~sderr) matrices.
    matrices collected_point_fc_and_sderr = set_model_estimate_and_forecast(&cv, &self)

    bundle forecasts_and_factors = \
      point_fc_and_factor_as_arrays(collected_point_fc_and_sderr, &self)

    # Retrieve moving-window point_forecasts from array,
    # compute forecast intervals and put stuff to bundle
    if self.endogenous_in_logs == FALSE || self.y_is_constant == TRUE
        return compute_intval_forecasts(forecasts_and_factors)
    elif self.endogenous_in_logs == TRUE && self.y_is_constant == FALSE
        bundle interval_forecasts = compute_intval_forecasts(forecasts_and_factors)
        bundle intval_fc_in_levels = intval_forecasts_to_levels(interval_forecasts,\
          collected_point_fc_and_sderr, self)
        return intval_fc_in_levels
    endif
end function


# TODO: untested
function bundle compute_intval_forecasts (const bundle forecasts_and_factors)
/* Generate arrays holding interval forecasts for each model based
    on respective point forecasts and estimated standard derrors. */

    bundle interval_forecasts

    matrix interval_forecasts.forecasts_point = \
      flatten(forecasts_and_factors.point_forecasts)	# rows: horizons, cols: sample period

    matrix factors_flattened = flatten(forecasts_and_factors.factors)

    matrix interval_forecasts.forecasts_lower_bound = \
      interval_forecasts.forecasts_point .- factors_flattened

    matrix interval_forecasts.forecasts_upper_bound = \
      interval_forecasts.forecasts_point .+ factors_flattened

    return interval_forecasts
end function

# TODO: untested
function bundle intval_forecasts_to_levels (const bundle interval_forecasts,
                                            const matrices collected_point_fc_and_sderr,
                                            const bundle self)
    /* Once interval forecasts are computed but the endogenous was transformed to
    logarithms, we need to compute the levels of the interval forecasts. */

    bundle in_levels
    scalar m = nelem(collected_point_fc_and_sderr)	# number of moving-windows
    matrix in_levels.forecasts_point = NA * zeros(self.horizon, m)
    matrix in_levels.forecasts_lower_bound = NA * zeros(self.horizon, m)
    matrix in_levels.forecasts_upper_bound = NA * zeros(self.horizon, m)

    loop i=1..m -q
        in_levels.forecasts_point[,i] = \
          log_linear_to_levels(interval_forecasts.forecasts_point[,i],\
          collected_point_fc_and_sderr[i][,2])

        in_levels.forecasts_lower_bound[,i] = \
          log_linear_to_levels(interval_forecasts.forecasts_lower_bound[,i],\
          collected_point_fc_and_sderr[i][,2])

        in_levels.forecasts_upper_bound[,i] = \
          log_linear_to_levels(interval_forecasts.forecasts_upper_bound[,i],\
          collected_point_fc_and_sderr[i][,2])
    endloop

    return in_levels
end function


# TODO: untested
function matrix log_linear_to_levels (const matrix forecast "Point forecast",
                                      const matrix forecast_stderr "Forecast standard error")
    /* In case the endogenous was transformed to logarithms, retrieve the level.
       Important assumptions made:
       1) You want to forecast via conditional expectation (ie with quadratic loss)
       2) Data are at least approximately normal
       Read: https://davegiles.blogspot.com/2013/08/forecasting-from-log-linear-regressions.html
    */

    return exp( forecast + 0.5 * forecast_stderr.^2 )
end function


# TODO: untested
function matrices set_model_estimate_and_forecast (bundle *cv, bundle *self)
/* Loop over training sets, run estimation and retrieve
    interval forecasts. */

    matrices collected_point_fc_and_sderr = array(cv.n_folds)

    loop lwin=1..cv.n_folds -q
        matrix date_idx = cv.X_train[lwin]

        # time index of the training set = self.window_length + self.horizon
        # As the last rolling samples may inlude only a sample set less than self.horizon
        # observations, we still can compute forecasts for $Ttest < self.horizon
        scalar obs_left = $tmax - maxc(date_idx[,1])
        scalar self.horizon_current = xmin(self.horizon, obs_left)

        if self.horizon_current == 0
            break
        endif

        smpl minc(date_idx[,1]) (maxc(date_idx[,1]) + self.horizon_current)

        if lwin == 1
            # CV series holds "window_length+horizon" observations: subtract "horizon"
            self.t1_train_date = obslabel($t1)
            string self.t2_train_date = obslabel($t2 - self.horizon_current)
            string self.t1_forecast_date = obslabel($t2 - self.horizon_current + 1)	# Date of 1st h=1 ahead forecast

        elif lwin == (cv.n_folds - 1) && self.type_moving_window != "static"
            self.t2_forecast_date = self.t2_date_y		# Date of last h-step ahead forecast
        endif

        # h by 2 matrix (point_fc ~ sderr)
        collected_point_fc_and_sderr[lwin] = set_xlist_and_forecast(&self)
    endloop

    if self.type_moving_window == "static"
        string self.t2_forecast_date = obslabel(obsnum(self.t2_train_date) + self.horizon)	# Date of last h-step ahead forecast
    endif

    return collected_point_fc_and_sderr
end function


# TODO: untested
# Public function
function list get_naive_forecasts (const bundle self,
                                   matrix horizons[null] "Select specific horizons, moving-window only",
                                   const string suffix[null] "Suffix for series' names")
    /* Transform interval forecasts into a list of series and return this. */

    list R = null
    if !exists(suffix)
        string suffix = ""
    endif
    if !exists(horizons)
        matrix horizons = seq(1, self.horizon)
    endif
    matrix hors = vec(horizons)

    scalar err = horizons_isok(self, hors)
    if !err
        return R
    endif

    smpl obsnum(self.t1_forecast_date) obsnum(self.t2_forecast_date)	# don't drop

    return list_of_forecasts(self, hors, suffix)
end function

# TODO: add convenient function "get_naive_forecast_errors()"


# untested
function void plot_naive_forecasts (const bundle self "Bundle returned from naiveFC()",
      const matrix horizons[null] "Select only specific horizons",
      const string model[null] "Select a model in case of avgfc")
    /* Function for plotting realizations and interval forecasts. */

    if self.which == "avgfc" && !exists(model)
        funcerr "You must specify for which model to plot the forecasts."
    endif
    series y = self.y
    setinfo y --graph-name="Actuals"
    # re-set missing values back to NA
    series y = (y == replace_nan_value_by() && self.type_moving_window == "static") \
      ? NA : y

    if !exists(horizons)
        matrix horizons = seq(1, self.horizon)
    endif

    list plotlist = y get_naive_forecasts(self, horizons)
    list plotlist = get_specific_model_for_avgfc(self, plotlist, model)

    # incl. some pre-forecast periods obs. for plotting
    scalar start = xmax(obsnum(self.t1_forecast_date) - self.pre_fc_periods_to_plot, \
      1)
    smpl start obsnum(self.t2_forecast_date)

    call_plot(plotlist, self)
end function


# untested
function list get_specific_model_for_avgfc (const bundle self,
                                            const list plotlist,
                                            string model)
    /* In case of "avgfc" we only plot the results for a specific model
    but not for all in a single plot. */

    if self.which == "avgfc"
        string model = tolower(model)
        list plotlist = get_model_forecasts_from_list(plotlist, model)
        if nelem(plotlist) == 0
            funcerr "Model requested is not supported."
        endif
    endif

    return plotlist
end function


# untested
function list get_model_forecasts_from_list (const list plotlist,
                                             const string model)
    /* Retrieve from list only those series which include
    model in its serie's name. */

    list new = null
    loop foreach i plotlist -q
        if instring(varname(plotlist.$i), sprintf("_%s", model))
            list new += $i
        endif
    endloop

    return new
end function


# untested
function void call_plot (const list plotlist,
                         const bundle self)
    /* */

    string filename = self.filename
    scalar ylo
    scalar yup
    yaxis_ranges(plotlist, &ylo, &yup)

    plot plotlist
        options with-lp time-series single-yaxis
        literal set linetype 1 lc rgb "black" lw 1 pt 0 ps 0
        literal set linetype 3 lc rgb "red" lw 1.5 ps 0
        literal set linetype 2 lc rgb "grey" lw 1.5 ps 0
        literal set linetype 4 lc rgb "grey" lw 1.5 ps 0
        literal set key left bottom below
        #literal set key inside left bottom
        #put the border more to the background by applying it
        # only on the left and bottom part and put it and the tics in gray
        literal set style line 11 lc rgb "#808080" lt 1
        literal set border 3 back ls 11			# get rid of upper + left border
        literal set tics nomirror
        #add a slight grid to make it easier to follow the exact position of the curves
        literal set style line 12 lc rgb "#808080" lt 0 lw 1	# light grey color
        literal set grid back ls 12
        printf "set yrange[%g:%g]", ylo, yup
        printf "set ylabel \"%s\"", self.ylab
        printf "set xlabel \"%s\"", self.xlab
        printf "set title \"%s\"", self.title
    end plot --output="@filename"
end function


# FIXME
function matrix GUI_naiveFC (const series y "Dependent variable",
                             const list xlist[null] "Additional exogenous (future values must be known)",
      int which[1:13:1] "Model type" {"meanFC", "medianFC", "rwFC", "rwdriftFC", \
      "ar1FC", "ar1trendFC", "ar1ma1fc", "smeanFC", "smedianFC", "snaiveFC", "snaivedriftFC", "avgFC", "airlinefc"},
      int h[1::10] "Forecast horizon",
      bool Plot[1] "Plot forecast")
    /* Helper function for GUI access. */

    # map integer value to string
    strings methods = get_supported_nonseas_methods()

    bundle opts = null
    bundle self = null
    opts.h = h
    opts.xlist = xlist
    self = naiveFC(y, methods[which], opts)

    # output
    printf "%12.4f\n", self.fc
    if Plot
        naivePlot(&self)
    endif

    return self.fc
end function
